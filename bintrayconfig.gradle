// Set default
ext.modulePackageId = ""

if (!project.rootProject.file('local.properties').exists()) {
    println "Not applying bintrayconfig.gradle"
    return
}
apply plugin: 'com.novoda.bintray-release'

def getBintrayUserAndKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return [
            properties.getProperty("bintray.user"),
            properties.getProperty("bintray.apikey")
    ]
}

if (versions == null || versions.publish == null) {
    throw new IllegalStateException("Unable to reference publishVersion")
}

task checkBintrayConfig {
    doLast {
        def (user, key) = getBintrayUserAndKey()
        if (user == null || user.isEmpty() ||
                key == null || key.isEmpty()) {
            throw new IllegalStateException("Must specify Bintray user/API key in your local.properties.")
        }
    }
}

afterEvaluate {
    bintrayUpload.dependsOn checkBintrayConfig
}

def (user, key) = getBintrayUserAndKey()
publish {
    bintrayUser = user
    bintrayKey = key
    userOrg = 'seanghay'
    groupId = 'com.seanghay'
    artifactId = 'console'
    publishVersion = versions.publish
    desc = 'A simple yet concise console.log for Android'
    website = 'https://github.com/seanghay/console'
    dryRun = false
}

ext.modulePackageId = publish.groupId + '.' + publish.artifactId